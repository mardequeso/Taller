programa Ej4
procesos
  proceso juntarFlores
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
  fin
  proceso juntarPapeles
  comenzar
    mientras(HayPapelEnLaEsquina)
      tomarPapel
  fin
  proceso vaciarBolsa
  comenzar
    mientras(HayFlorEnLaBolsa)
      depositarFlor
    mientras(HayPapelEnLaBolsa)
      depositarPapel
  fin
areas
  area1:AreaP(2,1,2,1)
  area2:AreaP(3,1,3,1)
  areaJ:AreaP(1,1,1,1)
  general:AreaPC(2,2,100,100)
robots
  robot esclavo
  variables
    id,tarea,av,ca,avOriginal,caOriginal:numero
  comenzar
    avOriginal:=PosAv
    caOriginal:=PosCa
    RecibirMensaje(id,rJ)
    RecibirMensaje(tarea,rJ)
    mientras(tarea<>4)
      RecibirMensaje(av,rJ)
      RecibirMensaje(ca,rJ)
      BloquearEsquina(av,ca)
      Pos(av,ca)
      si(tarea=1)
        juntarFlores
      sino
        si(tarea=2)
          juntarPapeles
        sino
          vaciarBolsa
      Pos(avOriginal,caOriginal)
      LiberarEsquina(av,ca)
      EnviarMensaje(V,rJ)
      RecibirMensaje(tarea,rJ)
  fin
  robot jefe
  variables
    r,tarea,av,ca:numero
    listo:boolean
  comenzar
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    repetir 10
      Random(r,1,2)
      Random(tarea,1,3)
      Random(av,2,100)
      Random(ca,2,100)
      si(r=1)
        EnviarMensaje(tarea,r1)
        EnviarMensaje(av,r1)
        EnviarMensaje(ca,r1)
        RecibirMensaje(listo,r1)
      sino
        EnviarMensaje(tarea,r2)
        EnviarMensaje(av,r2)
        EnviarMensaje(ca,r2)
        RecibirMensaje(listo,r2)
    EnviarMensaje(4,r1)
    EnviarMensaje(4,r2)
  fin
variables
  r1,r2:esclavo
  rJ:jefe
comenzar
  AsignarArea(rJ,areaJ)
  AsignarArea(r1,area1)
  AsignarArea(r1,general)
  AsignarArea(r2,area2)
  AsignarArea(r2,general)
  Iniciar(rJ,1,1)
  Iniciar(r1,2,1)
  Iniciar(r2,3,1)
fin
{Un robot jefe asigna tareas a los trabajadores. Las tareas consisten en 1. recoger flores, 2. recoger papeles, 3. vaciar bolsa, 4. finalizar . Existen 2 robots 
trabajadores que reciben solicitudes de tareas del robot jefe. Para cada solicitud, reciben la tarea y la esquina donde deben realizarla (salvo cuando la tarea es
4 que no deben acceder a una esquina). Luego de recibir la tarea, los robots van a la esquina indicada, realizan la tarea, avisan al jefe que ya la completaron y 
quedan a la espera de una nueva tarea. El robot jefe inicia en la esquina (1,1) y los robots trabajadores inician en las esquinas (2,1) y (3,1). Las tareas se 
asignan aleatoriamente a cualquier esquina dentro del cuadrante comprendido entre las esquinas (2,2) y (100,100). El robot jefe envía 10 tareas aleatorias (entre 1
y 3) a trabajadores aleatorios y termina. Al finalizar el jefe envía la tarea 4. Analice: existe el riesgo de que el programa quede bloqueado, y que ningún robot 
trabajador pueda realizar su tarea. ¿en qué caso puede suceder esto? ¿qué resulta necesario considerar para evitar esta situación?
