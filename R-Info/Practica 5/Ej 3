programa Ej3
procesos
  proceso dejarFlores(ES flores:numero;E n:numero)
  variables
    av,ca:numero
  comenzar
    av:=PosAv
    ca:=PosCa
    BloquearEsquina(10,10)
    Pos(10,10)
    mientras(HayFlorEnLaBolsa)
      depositarFlor
    Pos(av,ca)
    LiberarEsquina(10,10)
    EnviarMensaje(n,rJ)
    EnviarMensaje(V,rJ)
    EnviarMensaje(flores,rJ)
    flores:=0
  fin
  proceso tomarFlores(E flores:numero;E av:numero; E ca:numero)
  comenzar
    BloquearEsquina(10,10)
    Pos(10,10)
    repetir flores
      tomarFlor
    Pos(av,ca)
    LiberarEsquina(10,10)
  fin
  proceso juntarFlores(ES flores:numero; E n:numero)
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
      flores:=flores+1
      si(flores=10)
        dejarFlores(flores,n)
  fin
  proceso calle(E n:numero)
  variables
    flores,esquinas:numero
  comenzar
    flores:=0
    esquinas:=0
    repetir 99
      juntarFlores(flores,n)
      esquinas:=esquinas+1
      si(esquinas=15)
        dejarFlores(flores,n)
        esquinas:=0
      mover
    juntarFlores(flores,n)
  fin
areas
  area1:AreaP(1,3,100,3)
  area2:AreaP(1,4,100,4)
  areaJ:AreaP(1,5,1,5)
  general:AreaC(10,10,10,10)
robots
  robot recolector
  variables
    n: numero
  comenzar
    derecha
    RecibirMensaje(n,rJ)
    calle(n)
    EnviarMensaje(n,rJ)
    EnviarMensaje(F,rJ)
  fin
  robot cosechador
  variables
    r,flores,cant,terminaron,av,ca:numero
    sigue:boolean
  comenzar
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    flores:=0
    terminaron:=0
    av:=PosAv
    ca:=PosCa
    mientras(terminaron<2)
      RecibirMensaje(r,*)
      si(r=1)
        RecibirMensaje(sigue,r1)
        si(sigue)
          RecibirMensaje(cant,r1)
          tomarFlores(cant,av,ca)
          flores:=flores+cant
        sino
          terminaron:=terminaron+1
      sino
        RecibirMensaje(sigue,r2)
        si(sigue)
          RecibirMensaje(cant,r2)
          tomarFlores(cant,av,ca)
          flores:=flores+cant
        sino
          terminaron:=terminaron+1
    Informar("la cantidad de flores juntadas fue de ",flores)
  fin
variables
  r1,r2:recolector
  rJ:cosechador
comenzar
  AsignarArea(r1,area1)
  AsignarArea(r1,general)
  AsignarArea(r2,area2)
  AsignarArea(r2,general)
  AsignarArea(rJ,areaJ)
  AsignarArea(rJ,general)
  Iniciar(r1,1,3)
  Iniciar(r2,1,4)
  Iniciar(rJ,1,5)
fin
{3- Dos robots recolectores avanzan por las calles 3 y 4 respectivamente juntando todas las flores a su paso. Cada esquina tiene a lo sumo una flor. Cada vez que 
juntan 10 flores o que avanzan 15 esquinas, deberán vaciar de flores su bolsa en el depósito localizado en la esquina (10,10). Cada vez que se depositan flores en
el depósito, un robot cosechador deberá juntar dichas flores. Cuando ambos recolectores hayan completado sus calles, el robot cosechador deberá informar la 
cantidad de flores recolectadas.Los recolectores inician en la esquina (1,3) y (1,4), y el cosechador en la esquina (1,5)
