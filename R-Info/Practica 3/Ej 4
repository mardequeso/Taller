programa Ej4
areas
  esquinaDejar: AreaC(11,11,11,11)
  esquinaJuntar: AreaPC(10,10,10,10)
  areaJ: AreaP(1,1,1,1)
  area1: AreaP(9,9,9,9)
  area2: AreaP(9,10,9,10)
  area3: AreaP(9,11,9,11)
  area4: AreaP(9,12,9,12)
robots
  robot limpiador
  variables
    n, avOriginal, caOriginal:numero
    seguir:boolean
  comenzar
    seguir:=V
    avOriginal:= PosAv
    caOriginal:= PosCa
    RecibirMensaje(n,rJ)
    mientras(seguir)
      BloquearEsquina(10,10)
      Pos(10,10)
      si(HayFlorEnLaEsquina)
        tomarFlor
        BloquearEsquina(11,11)
        Pos(11,11)
        LiberarEsquina(10,10)
        depositarFlor
        Pos(avOriginal,caOriginal)
        LiberarEsquina(11,11)
      sino
        seguir:= F
        Pos(avOriginal,caOriginal)
        LiberarEsquina(10,10)
    EnviarMensaje(n, rJ)
  fin
  robot jefe
  variables
    n, ultimo: numero
  comenzar
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    EnviarMensaje(3,r3)
    EnviarMensaje(4,r4)
    repetir 4
      RecibirMensaje(n,*)
    ultimo:= n
    Informar("El ultimo fue ", ultimo)
    BloquearEsquina(11,11)
    Pos(11,11)
    mientras(HayFlorEnLaEsquina)
      tomarFlor
    Pos(1,1)
    LiberarEsquina(11,11)
  fin
variables
  r1,r2,r3,r4: limpiador
  rJ: jefe
comenzar
  AsignarArea(r1,area1)
  AsignarArea(r1,esquinaJuntar)
  AsignarArea(r1,esquinaDejar)
  AsignarArea(r2,area2)
  AsignarArea(r2,esquinaJuntar)
  AsignarArea(r2,esquinaDejar)
  AsignarArea(r3,area3)
  AsignarArea(r3,esquinaJuntar)
  AsignarArea(r3,esquinaDejar)
  AsignarArea(r4,area4)
  AsignarArea(r4,esquinaJuntar)
  AsignarArea(r4,esquinaDejar)
  AsignarArea(rJ,areaJ)
  AsignarArea(rJ,esquinaDejar)
  Iniciar(r1,9,9)
  Iniciar(r2,9,10)
  Iniciar(r3,9,11)
  Iniciar(r4,9,12)
  Iniciar(rJ,1,1)
fin
{4- Realice un programa en el que 4 robots mueven de a una todas las flores de la esquina (10,10) a la esquina (11,11). Para ello, cada robot que toma una flor de 
la esquina (10,10) la deposita en la esquina (11,11) y luego retorna a su esquina inicial. Cada robot que finaliza (o, sea, que detecta que la esquina (10,10) se ha
vaciado) deberá avisar al robot coordinador que ha finalizado. Cuando todos los robots finalizaron, el robot coordinador deberá informar qué robot finalizó último y
a continuación deberá recolectar todas las flores de la esquina (11,11). El robot coordinador inicia en la esquina (1,1). Los robots inician en las esquinas (9,9) 
(9,10) (9,11) y (9,12).
