programa Ej5
areas
  fuente: AreaPC(11,11,11,11)
  area1: AreaP(4,1,4,100)
  area2: AreaP(6,1,6,100)
  area3: AreaP(8,1,8,100)
  area4: AreaP(10,1,10,100)
  areaJ:AreaP(1,1,1,1)
robots
  robot competidor
  variables
    av,ca,n:numero
    sigo: boolean
  comenzar
    sigo:= V
    RecibirMensaje(n,rJ)
    mientras(sigo)
      av:= PosAv
      ca:= PosCa
      BloquearEsquina(11,11)
      Pos(11,11)
      si(HayPapelEnLaEsquina)
        tomarPapel
      sino
        sigo:= F
      Pos(av,ca)
      LiberarEsquina(11,11)
      si (sigo)
        depositarPapel
        si (PosCa<100)
          mover
        sino
          sigo:= F
    EnviarMensaje(n,rJ)
    EnviarMensaje(ca,rJ)
  fin
  robot jefe
  variables
    n, ca, masLejos, ganador: numero
  comenzar
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    EnviarMensaje(3,r3)
    EnviarMensaje(4,r4)
    masLejos:= 0
    repetir 4
      RecibirMensaje(n,*)
      si (n=1)
        RecibirMensaje(ca,r1)
      sino
        si(n=2)
          RecibirMensaje(ca,r2)
        sino
          si (n=3)
            RecibirMensaje(ca,r3)
          sino
            RecibirMensaje(ca,r4)
      si(ca > masLejos)
        masLejos:= ca
        ganador:= n
    si (masLejos > 0)
      Informar("El ganador fue ",ganador)
  fin
variables
  r1,r2,r3,r4: competidor
  rJ: jefe
comenzar
  AsignarArea(r1, fuente)
  AsignarArea(r1, area1)
  AsignarArea(r2, fuente)
  AsignarArea(r2, area2)
  AsignarArea(r3, fuente)
  AsignarArea(r3, area3)
  AsignarArea(r4, fuente)
  AsignarArea(r4, area4)
  AsignarArea(rJ, areaJ)
  Iniciar(r1,4,1)
  Iniciar(r2,6,1)
  Iniciar(r3,8,1)
  Iniciar(r4,10,1)
  Iniciar(rJ,1,1)
fin
{5- Realice un programa en el que 4 robots juegan una carrera por avenidas diferentes: 4, 6, 8 y 10 respectivamente. Todos los robots inician en la calle 1. Para 
poder avanzar, cada robot debe juntar un papel de una fuente de papeles localizada en la esquina (11,11), colocarlo en la esquina actual de su avenida y avanzar un 
paso. Cuando la esquina fuente ya no tiene más papeles, o cuando se haya completado la avenida, deberán avisar al robot coordinador y este determinará el robot que 
llegó más lejos.
